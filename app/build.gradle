plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
}

def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(rootProject.file("keystore.properties")))

android {
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdk 35
    defaultConfig {
        applicationId "com.coresoftwaredesign.ninjawin"
        minSdkVersion 21
        targetSdkVersion 35
        versionCode 9
        versionName "1.0.9"

        // testing on Firebase Test Lab, etc. Disables Ads. Set "false" for production release
        buildConfigField "boolean", "ENABLE_TEST_BUILD", "false"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }
    namespace 'com.coresoftwaredesign.ninjawin'
    buildFeatures {
        buildConfig true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.7.0'

    // Add the Firebase Crashlytics SDK
    implementation 'com.google.firebase:firebase-crashlytics:19.4.1'
    // Add the Google Analytics SDK
    implementation 'com.google.firebase:firebase-analytics:22.3.0'

    // Google Ads
    implementation 'com.google.android.gms:play-services-ads:23.6.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'

    // debugging
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.14'
}

secrets {
    // Change the properties file from the default "local.properties" in your root project
    // to another properties file in your root project.
    propertiesFileName 'secrets.properties'
    // A properties file containing default secret values. This file can be checked in version
    // control.
    defaultPropertiesFileName = 'secrets.defaults.properties'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}
